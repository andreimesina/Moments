apply plugin: 'com.android.application'

// app version
ext.majorVersion = 0
ext.minorVersion = 9
ext.patchVersion = 1
ext.preRelease = "alpha.1"

// sdk and tools
ext.minSdkVersion = 21
ext.targetSdkVersion = 28
ext.compileSdkVersion = 28
ext.buildToolsVersion = '3.3.1'

ext.supportLibraryVersion = '28.0.0'
ext.firebaseVersion = '16.0.1'

ext.jUnitVersion = '4.12'

android {
    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        applicationId "com.andreimesina.moments"
        targetSdkVersion project.targetSdkVersion
        minSdkVersion project.minSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

private Integer generateVersionCode() {
    return ext.minSdkVersion * 10000000 + ext.majorVersion * 10000 + ext.minorVersion * 100 + ext.patchVersion
}

private String generateVersionName() {
    String versionName = "${ext.majorVersion}.${ext.minorVersion}.${ext.patchVersion}"
    versionName += "-" + ext.preRelease

    return versionName;
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$project.supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.android.support:recyclerview-v7:$project.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$project.supportLibraryVersion"
    implementation "com.android.support:design:$project.supportLibraryVersion"
    implementation 'de.hdodenhof:circleimageview:3.0.0'

    // Firebase
    implementation "com.google.firebase:firebase-core:16.0.7"
    implementation "com.google.firebase:firebase-auth:16.1.0"

    // Google Play services
    implementation 'com.google.android.gms:play-services-auth:16.0.1'

    testImplementation "junit:junit:$project.jUnitVersion"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'org.jetbrains:annotations-java5:15.0'
}

apply plugin: 'com.google.gms.google-services'
